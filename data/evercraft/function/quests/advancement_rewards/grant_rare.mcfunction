# ============================================================
# Grant Rare Crate from Advancement
# ============================================================

function evercraft:quests/advancement_rewards/check_iron_gate
execute unless score @s ec.iron_gate_passed matches 1 run return fail

# Check inventory space before giving
execute store result score #inv_full ec.var run function evercraft:util/check_inv_full

execute if score #inv_full ec.var matches 0 run loot give @s loot evercraft:achievements/crates/rare
execute if score #inv_full ec.var matches 1 at @s run loot spawn ~ ~ ~ loot evercraft:achievements/crates/rare
experience add @s 50 points

playsound minecraft:entity.experience_orb.pickup player @s ~ ~ ~ 0.5 1.4
tellraw @s ["",{text:"[Advancement] ",color:"green"},{text:"You received a ",color:"gray"},{text:"Rare Achievement Crate",color:"aqua"},{text:" + ",color:"gray"},{text:"50 XP",color:"green"},{text:"!",color:"gray"}]
execute if score #inv_full ec.var matches 1 run tellraw @s [{text:"[!] ",color:"red"},{text:"Inventory full! Your crate was dropped at your feet.",color:"yellow"}]
execute if score #inv_full ec.var matches 1 run playsound minecraft:block.note_block.bass player @s ~ ~ ~ 1 0.5

# Track total achievements earned
scoreboard players add @s ach.total 1
